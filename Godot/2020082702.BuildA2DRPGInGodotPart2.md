# 如何制作2D RPG游戏 - Part 2- Godot3教程

![](https://www.colorgamer.com/usr/uploads/2020/08/512727219.png)

> Godot游戏开发新手教程 #3
>
> 翻译自：[Godot Game Development for Beginner #3](https://godottutorials.pro/rpg-godot-tutorial/) 
>
> 作者：Daniel Buckley



## 目录

1. 介绍
2. 项目文件
3. 摄像机跟随
4. 创建Enemy
5. 编写Enemy脚本
6. Player函数
7. Player互动
8. 可互动的箱子
9. 创建UI
10. 编写UI脚本
11. 结论



## 介绍

在本系列教程的Part 1，我在免费开源的Godot引擎里创建了我们的2D RPG项目。我们学习了一些内容，如：tilemaps, player控制器，raycasting，sprite动画等等！总而言之，我们有了一个很好的基础！

然而，什么RPG游戏会没有敌人或物品拾取呢？在本系列的Part 2，我们将添加它们，同时为摄像机添加一些能力，让它可以跟随player。



## 项目文件

对于本项目，我们需要一些素材，如果精灵，字体。它们来自于[kenney.nl](https://kenney.nl/)和[Google Fonts](https://fonts.google.com/)。当然，你也可以使用自己的资源，但在本教程里，我们将使用它们。

- 下载精灵和字体素材以及完整的项目 [点这里](https://godottutorials.pro/rpg-godot-tutorial/)



## 摄像机跟随

在我们的大场景中，有一个问题是摄像机不会动。要解决这个问题，让我们在**MainScene**中创建一个新的节点**Camera2D**。

![](https://www.colorgamer.com/usr/uploads/2020/08/1257603988.png)

接着为它创建一个新的脚本，叫作**CameraFollow**。在这里我们要做的是在每帧中找到player并跟着他。

```
onready var target = get_node("/root/MainScene/Player")
 
func _process (delta):
    position = target.position
```

最后，在inspector中，将**Current**设置为**on**，以便我们通过它来看场景内。

![](https://www.colorgamer.com/usr/uploads/2020/08/3867092566.png)

现在，如果按下play，你可以看到摄像机跟随着player。



## 创建Enemy

让我们来创建一个enemy scene。它们将在一定的范围里追捕player。

1. 使用**KinematicBody2D**节点为根节点创建一个新的scene
2. 重命名它为**Enemy**并保存
3. 将**player_s_0.png** sprite拖入场景中，创建一个sprite节点
4. 将**Modulate**设置为*red*
5. 创建一个shape为capsule的**CollisionShap2D**节点，并设置它的大小
6. 创建一个**Timer**节点 - 它将用于检测何时可以发起攻击

![](https://www.colorgamer.com/usr/uploads/2020/08/491885693.png)



## 编写Enemy脚本

为enemy节点创建一个脚本，叫**Enemy**。然后从变量开始。

```
var curHp : int = 5
var maxHp : int = 5
 
var moveSpeed : int = 150
var xpToGive : int = 30
 
var damage : int = 1
var attackRate : float = 1.0
var attackDist : int = 80
var chaseDist : int = 400
 
onready var timer = $Timer
onready var target = get_node("/root/MainScene/Player")
```

在**_physics_process**(每秒会调用60次)函数里，如果比可攻击的距离远，但接近追逐的距离时，我们想要朝目标移动。

```
func _physics_process (delta):
 
    var dist = position.distance_to(target.position)
 
    if dist > attackDist and dist < chaseDist:
        var vel = (target.position - position).normalized()
 
        move_and_slide(vel * moveSpeed)
```

回到**MainScene**，将**Enemy** scene拖入场景中，它会创建一个实例。现在，可以按下play，测试一下。enemy会朝我们跑来。

![](https://www.colorgamer.com/usr/uploads/2020/08/4288020306.png)

现在，enemy朝我们移动了，接着让它攻击我们。

1. 打开**Enemy** scene
2. 选择**Timer**节点
3. 在Inspector面板里，点击**Node**标签
4. 双击**timeout()**信号
5. 点击**Connect**

![](https://www.colorgamer.com/usr/uploads/2020/08/1275900604.png)

这会创建**_on_Timer_timeout**函数。在该函数里，我们想检测如果进入可攻击距离，则发起攻击。我们将后面创建**take_damage**函数。

```
func _on_Timer_timeout ():
 
    if position.distance_to(target.position) <= attackDist:
        target.take_damage(damage)
```

在**_ready**（节点初始化后，会被调用一次）函数里，设置一下timer节点。

```
func _ready ():
 
    timer.wait_time = attackRate
    timer.start()
```

最后，让我们创建**take_damage**和**die**函数。**target.give_xp**函数将在后面的player脚本里创建。

```
func take_damage (dmgToTake):
 
    curHp -= dmgToTake
 
    if curHp <= 0:
        die()
 
func die ():
 
    target.give_xp(xpToGive)
    queue_free()
```



## Player函数

现在，让我们打开**Player**脚本，并添加一些新的函数用于和enemies互动。让我们从**give_gold**函数开始，它将为player增加金币。

```
func give_gold (amount):
 
    gold += amount
```

对于等级系统，我们创建**give_xp**和**level_up**函数。

```
func give_xp (amount):
 
    curXp += amount
 
    if curXp >= xpToNextLevel:
        level_up()
 
func level_up ():
 
    var overflowXp = curXp - xpToNextLevel
 
    xpToNextLevel *= xpToLevelIncreaseRate
    curXp = overflowXp
    curLevel += 1
```

**take_damage**和**die**函数会在enmey攻击player时被调用。

```
func take_damage (dmgToTake):
 
    curHp -= dmgToTake
 
    if curHp <= 0:
        die()
 
func die ():
 
    get_tree().reload_current_scene()
```

现在，按下play，你可以看到enemy可以攻击和杀死我们，之后场景会重新加载。



## Player互动

对于player，我们希望与箱子和敌人进行互动。在**Player**脚本里，让我们创建**_process**函数(每帧都会被调用)，监听我们按下互动按钮。

```
func _process (delta):
 
    if Input.is_action_just_pressed("interact"):
        try_interact()
```

**try_interact**函数将检测raycat是否触碰到任何物体。如果是enmey，攻击它们。如果不是，调用**on_interact**函数，如果对象有这个函数的话。

```
func try_interact ():
 
    rayCast.cast_to = facingDir * interactDist
 
    if rayCast.is_colliding():
        if rayCast.get_collider() is KinematicBody2D:
            rayCast.get_collider().take_damage(damage)
        elif rayCast.get_collider().has_method("on_interact"):
            rayCast.get_collider().on_interact(self)
```



## 可互动的箱子

现在，我们需要一些可以互动的物体。它将是一个可以给player一些金子的箱子。使用**Area2D**为根节点创建一个新的scene。

1. 重命名它为**Chest**
2. 保存场景
3. 将**rpgTile163.png**拖入场景中以创建一个Sprite节点
4. 创建**CollisionShape2D**节点
5. 设置shape为*Rectangle*，并设置大小以覆盖sprite

![](https://www.colorgamer.com/usr/uploads/2020/08/2859830293.png)

接着，为Area2D节点创建一个新脚本**Chest**。我们要在这里做的事情就是，使用**on_interact**函数给player金子并销毁这个节点。

```
export var goldToGive : int = 5
 
func on_interact (player):
 
    player.give_gold(goldToGive)
    queue_free()
```

你可能已经注意到，和箱子的互动不会发生，但攻击enmey的会。因此，在**Player**场景里，选择raycast节点，将**Collide With Areas**勾上。

![](https://www.colorgamer.com/usr/uploads/2020/08/1052886857.png)



## 创建UI

使用**Control**为根节点创建一个新场景。

1. 重命名为**UI**
2. 保存场景
3. 创建一个新节点**TextureRect**，重命名为**BG**
4. 将**Texure**设置成*UI_Square.png*
5. 将**Expand**勾上
6. 将4个锚点拖到底部的中间
7. 设置**Position**为386, 520
8. 设置**Size**为250, 60
9. 设置**Visibility > Self Modulate**为*dark grey*

![](https://www.colorgamer.com/usr/uploads/2020/08/1045165767.png)

为**BG**节点添加3个新的子节点。

- LevelBG - **TextureRect**
- HealthBar - **TextureProgress**
- XpBar - **TextureProgress**

对于**TextureProgress**节点:

1. 勾上**Nine Patch Stretch**
2. 设置**Under**和**Progress** textures为*UI_Square.png*
3. 设置**Under** tint为*dark grey*
4. 设置**Progress** tint为*red* or *green*

![](https://www.colorgamer.com/usr/uploads/2020/08/3169730463.png)

现在，我们需要设置字体。在我们导入到项目的素材中，有两个字体文件。此时，它们是.ttf文件，需要转换为Godot可以识别的格式。对每个字体：

1. 右键点击它，并选择**New Resource...**
2. 选择**DynamicFont** resource
3. 保存到字体文件夹里
4. 双击新的资源并将.ttf文件拖入**Font > Font Data**属性里

![](https://www.colorgamer.com/usr/uploads/2020/08/2927199405.png)

为**LevelBG**节点创建一个子节点**Label**，并重命名为**LevelText**。

- 调整rect box的大小，填满level BG的大小
- 将**Align**和**Valign**设置为*Center*
- 将bold dynamic font resource拖入**Custom Fonts**属性里
- 设置font **Size**为35
- 设置**Visibility > Self Modulate**为*grey*

![](https://www.colorgamer.com/usr/uploads/2020/08/3567769698.png)

创建一个新的**Label**节点，命名为**GoldText**。

1. 移动，调整大小如上图
2. 设置**Align**为*Center*
3. 设置**Custom Font**为regular dynamic font resource



## 编辑UI脚本

接下来，我们为**UI**节点创建一个脚本，命名为**UI**。我们将从引用子节点的变量开始。

```
onready var levelText : Label = get_node("BG/LevelBG/LevelText")
onready var healthBar : TextureProgress = get_node("BG/HealthBar")
onready var xpBar : TextureProgress = get_node("BG/XpBar")
onready var goldText : Label = get_node("BG/GoldText")
```

接着，为更新这些节点添加函数。

```
# updates the level text Label node
func update_level_text (level):
 
    levelText.text = str(level)
 
# updates the health bar TextureProgress value
func update_health_bar (curHp, maxHp):
 
    healthBar.value = (100 / maxHp) * curHp
 
# updates the xp bar TextureProgress value
func update_xp_bar (curXp, xpToNextLevel):
 
    xpBar.value = (100 / xpToNextLevel) * curXp
 
# updates the gold text Label node
func update_gold_text (gold):
 
    goldText.text = "Gold: " + str(gold)
```

现在，我们有**UI**脚本，回到**MainScene**，将**UI**场景拖入其中。为了将它呈现在屏幕上，我们需要创建一个**CanvasLayer**节点，作为**UI**节点的父节点。

![](https://www.colorgamer.com/usr/uploads/2020/08/1208362839.png)

为了Player能够访问UI脚本，我们需要将CanvasLayer移动到顶部。

![](https://www.colorgamer.com/usr/uploads/2020/08/770447328.png)

在**Player**脚本中，我们创建一个引用UI脚本的变量。

```
onready var ui = get_node("/root/MainScene/CanvasLayer/UI")
```

创建**_ready**函数，它会在节点初始化时被调用一次。在函数里，我们将初始化UI。

```
func _ready ():
 
    ui.update_level_text(curLevel)
    ui.update_health_bar(curHp, maxHp)
    ui.update_xp_bar(curXp, xpToNextLevel)
    ui.update_gold_text(gold)
```

现在我们需要在一些函数的末尾调用UI函数，如下：

```
# give_gold function
ui.update_gold_text(gold)
 
# give_xp function
ui.update_xp_bar(curXp, xpToNextLevel)
 
# level_up function
ui.update_level_text(curLevel)
ui.update_xp_bar(curXp, xpToNextLevel)
 
# take_damage function
ui.update_health_bar(curHp, maxHp)
```

现在，我们可以点击play，可以看到游戏开始时，UI已经显示，而且在我们获取金子，受到伤害，获得经验和等级增长时，它们都会随之改变。

如上，我们在Godot中完成了RPG游戏。让我们回到**MainScene**里，在四处添加一些敌人和箱子。

![](https://www.colorgamer.com/usr/uploads/2020/08/2309508138.png)



## 结论

恭喜完成本教程！通过努力和一点专业的知识，我们使用Godot创建了具有许多不同功能的2D RPG游戏。通过Part 1和Part 2，我们向你展示了如何设置：

- 2D俯视角player控制器
- 可以攻击和追捕player的敌人
- 可以互动获取金子的箱子
- 显示HP，XP，Level和金子的UI

这样，你现在应该掌握了可以创建更多RPG游戏的基本技能！当然，你也可以将本游戏扩展 - 添加一些新的功能，完善当前的功能，甚至改善你认为不足的地方。这里可能有无限的可能，但有了这些基础，等于是有了一个好的开始。

非常感谢你的阅读。