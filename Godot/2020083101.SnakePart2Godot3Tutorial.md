# 贪食蛇 - Part 2 - Godot3教程

![](https://www.colorgamer.com/usr/uploads/2020/08/3005092090.png)

[Demo在这里](https://github.com/yuiitsu/Godot-Demo/tree/master/Snake)

接[Part 1](https://github.com/yuiitsu/Article/blob/master/Godot/2020083001.SnakeGodot3Tutorial.md)。

## 目录

1. 初始化食物
2. 移动蛇
   1. 自定义信号
   2. 定时器延时移动
3. 吃食物
4. 增加蛇身
5. 游戏结束



## 初始化食物

和初始化蛇一样，打开**MainScene**，添加**init_food**函数。

这里为什么不像**Snake**一样直接放在**init_entities**函数里呢？因为当我们吃掉一个食物后，还需要再初始化一个食物，且仅仅是初始化它。所以，将它放在单独的函数里方便调用。

```
func init_food():
	var food: Node2D = scene_food.instance() as Node2D
	add_child(food)
	grid.place_entity_at_random_pos(food)
```

接着在**init_entities**函数的最后添加:

```
init_food()
```

运行测试一下，蛇和食物都随机出现在了地图上。

![](https://www.colorgamer.com/usr/uploads/2020/08/2486895086.png)



## 移动蛇

首先我们要知道，在网格中移动本身和普通移动并没有什么区别，只是每次移动的距离为一个固定的单元格。贪食蛇最大的区别在于，对象都在数组里，每次移动，都需要修改数组里的数据，再将蛇放置在新的位置上。

打开**Snake** Scene，为**Snake**节点添加脚本，命名为**Snake**。我们仍然从变量开始。

```
class_name Snake

onready var direction = Vector2()
onready var body_segments: Array = [self]
```

- **class_name**：声明类**Snake**
- **direction**：移动的方向。蛇是自动移动的，所以，只需要指明一个方向
- **body_segments**：蛇身数组。[self]首先就将**Snake**（蛇头）加入到数组里

接着添加**_input**函数，用于监听方向控制。

```
func _input(event):
	var direction = Vector2()
	if event.is_action_pressed("ui_up"):
		direction = Vector2(0, -1)
	elif event.is_action_pressed("ui_down"):
		direction = Vector2(0, 1)
	elif event.is_action_pressed("ui_left"):
		direction = Vector2(-1, 0)
	elif event.is_action_pressed("ui_right"):
		direction = Vector2(1, 0)
```

再添加**_process**函数，我们要在这里处理蛇的移动。在写该方法前，我们需要知道**Snake**是怎么移动的。

![](https://www.colorgamer.com/usr/uploads/2020/08/398260367.png)

如上图，snake的移动就是从头到尾，每一个实体都往前移动一格。头和身体各实体的区别在于，头是移动到指定方向的那一格，而身体的各实体总是移动到上一个实体在移动前的位置上。从头到尾，我们的实体都在**body_segments**里。所以，移动时，遍历整个数组，将它们往前移动一格即可。

```
func _process(delta):
	if direction != Vector2():
		var pre_segment_pos: Vector2 = self.position
		for i in range(body_segments.size()):
			var temp_pos: Vector2 = Vector2()
			if i == 0:
				emit_signal("snake_move_triggered", body_segments[i], direction)
			else:
				temp_pos = body_segments[i].position
				emit_signal("body_segment_move_triggered", body_segments[i], pre_segment_pos)
				pre_segment_pos = temp_pos
```

- **if direction != Vector2()**：只有direction有值时再执行。游戏刚开始，没有按下任何方向键时，snake是不动的
- **pre_segment_pos**：前一实体的位置。每遍历一个实体就将它当前的位置附与它，这样下一个实体就知道自己应该往哪里去
- 遍历**body_segments**数组
- 当处理第一个实体（蛇头）时，发送信号，移动蛇头
- 当处理身体各实体时，发送信号，移动蛇身



### 自定义信号

**emit_signal**即为发出信号。处理自定义信号，需要做3件事：

1. 定义信号
2. 发射信号
3. 监听信号



#### 定义信号

```
signal snake_move_triggered(entity, direction)
signal body_segment_move_triggered(segment, segment_pos)
```

- **signal**：定义信号
- **snake_move_striggered**和**body_segment_move_triggered**都是信号名
- 小括号里为参数



#### 发射信号

```
emit_signal("snake_move_triggered", body_segments[i], direction)
emit_signal("body_segment_move_triggered", body_segments[i], pre_segment_pos)
```



#### 监听信号

```
snake.connect("snake_move_triggered", self, "_on_Snake_move_triggered")
```

这里只是给出自定义信号的3个函数。

打开**MainScene**脚本，在snake实例化后，添加信号监听。

```
func init_entities():
	snake = scene_snake.instance() as Node2D
	snake.connect("snake_move_triggered", self, "_on_Snake_move_triggered")
	snake.connect("body_segment_move_triggered", self, "_on_Snake_body_segment_move_triggered")
	# 后面的代码不变
```

**connect**有3个参数：

1. 监听信号名称
2. 处理对象，**self**即为本对象
3. 回调函数

因此，我们需要添加对应的两个回调函数：

```
func _on_Snake_move_triggered(entity: Node2D, direction: Vector2):
	grid.move_entity_in_direction(entity, direction)	

	
func _on_Snake_body_segment_move_triggered(segment: Node2D, segement_pos: Vector2):
	grid.move_entity_to_position(segment, segement_pos)
```

**move_entity_in_direction**和**move_entity_to_position**为**Grid**脚本的两个方法，我们接着添加。

打开**Grid**脚本，添加上面两个函数。

```
func move_entity_in_direction(entity: Node2D, direction: Vector2):
	var old_grid_pos: Vector2 = world_to_map(entity.position)
	var new_grid_pos: Vector2 = old_grid_pos + direction
	#
	set_entity_in_cell(null, old_grid_pos)
	place_entity(entity, new_grid_pos)

	
func move_entity_to_position(entity: Node2D, position: Vector2):
	var old_grid_pos: Vector2 = world_to_map(entity.position)
	var new_grid_pos: Vector2 = world_to_map(position)
	
	set_entity_in_cell(null, old_grid_pos)
	place_entity(entity, new_grid_pos)
```

它们的区别即在于，**move_entity_in_direction**是移动蛇头，它是需要按方向移动的，所以，**new_grid_pos**即为它原来位置加上**direction**，direction是一个向量，所以它是有方向的。

**set_entity_is_cell**：将指定位置的数组值清空（null）

**place_entity**：将实体放置到新的位置上

这时，可以按下运行按钮测试一下。你应该会发现，当按下方向键时，snake跑得太快了，这是因为**_process**每帧都会被调用，每秒60帧，所以移动得能不快吗。解决这个问题，我们需要添加一个新节点**Timer**。

- 打开**Snake** Scene
- 选中**Snake**节点，添加一个新的子节点**Timer**，命名为**MoveDelay**
- 设置**Wait Time**为0.1
- 选中**One Shot**和**Autostart**

![](https://www.colorgamer.com/usr/uploads/2020/08/1819448904.png)

回到**Snake**脚本，添加一个新的变量：

```
onready var can_move: bool = false
```

并在**_process**函数中添加条件**can_move**，只有当它为真是才能移动，接着是**can_move = false**和**$MoveDelay.start()**。之后的_process:

```
func _process(delta):
	if direction != Vector2() and can_move:
		var pre_segment_pos: Vector2 = self.position
		for i in range(body_segments.size()):
			var temp_pos: Vector2 = Vector2()
			if i == 0:
				emit_signal("snake_move_triggered", body_segments[i], direction)
			else:
				temp_pos = body_segments[i].position
				emit_signal("body_segment_move_triggered", body_segments[i], pre_segment_pos)
			pre_segment_pos = temp_pos
			
		# 延时移动
		can_move = false
		$MoveDelay.start()
```

这还不够，我们必须在延时结束时执行移动，所以：

- 选中**MoveDelay**节点
- 点开**Node**面板，双击**timeout()**信号，为**Snake**脚本添加一个新函数：

```
func _on_MoveDelay_timeout():
	can_move = true
```

函数很简单，就将can_move置为true，让snake可以移动一次。

运行测试一下，snake是不是慢下来了，如果还是觉得快，可以增加**Wait Time**值。



## 吃食物

当蛇头碰到食物时，食物消失，并在一个新的随机位置出现，即为吃食物。所以，我们需要有移动蛇头的函数里，检测是否碰到了食物。打开**Grid**脚本，在**move_entity_in_direction**函数里，添加代码：

```
var entity_of_new_cell: Node2D = get_entity_of_cell(new_grid_pos)
	if entity_of_new_cell != null:
		if entity_of_new_cell.is_in_group("Food"):
			emit_signal("eat_food", entity_of_new_cell, entity)
```

**get_entity_of_cell**：获取下一个位置的实体。如果有实体，进一步判断是否在**Food** group里，如果是表明碰到了食物，发射**eat_food**信号。

那么，这里，我们还差group和eat_food信号。

打开**Food** Scene，选中**Food**节点，来到**Node > Groups**面板，添加**Food** group。

回到**Grid**脚本，添加信号定义：

```
signal eat_food(food_entity, entity)
```

打开**MainScene** Scene，选中**Grid**节点，打开**Signal**面板，可以看到我们定义的**eat_food**信号已经出现在里面。

![](https://www.colorgamer.com/usr/uploads/2020/08/4481983.png)

双击它，为**MainScene**脚本添加新方法：

```
func _on_Grid_eat_food(food_entity, entity):
	if entity.has_method("eat_food"):
		entity.eat_food()
		food_entity.queue_free()
		init_food()
```

可以看到，参数**entity**即为**Snake**，所以，我们需要给**Snake**脚本添加**eat_food**方法，来到**Snake**脚本，添加新方法**eat_food**:

```
func eat_food():
	pass
```

此方法我们先留空。

- **queue_free**：即为消除实体，即食物

- **init_food**：即在新的随机位置生成一个食物

运行测试一下，吃掉一个食物后，新的食物又出现了。



## 增加蛇身

当吃到食物的时候，除了清除当前食物，生成新的食物外，还需要为Snake添加一格蛇身。非常简单，打开**Snake**脚本，添加一个新的变量：

```
const scene_tail = preload("res://Tail.tscn")
```

将**Tail** Scene预加载进来。来到**eat_food**函数，添加：

```
func eat_food():
	var tail_segment: Node2D = scene_tail.instance() as Node2D
	body_segments.append(tail_segment)
	#
	emit_signal("generated_tail_segment", tail_segment, body_segments[-2].position)
```

获得**Tail**实例后，就将它添加到**body_segments**数组里，接着发射信号**generated_tail_segment**，用于增加节点。因此，我们需要先定义信号，同样在**Snake**脚本：

```
signal body_segment_move_triggered(segment, segment_pos)
```

接着来到**MainScene**脚本，在获取snake实例后，监听信号：

```
snake.connect("generated_tail_segment", self, "_on_Snake_generated_tail_segment")
```

同时添加函数：

```
func _on_Snake_generated_tail_segment(segment: Node2D, segment_pos: Vector2):
	add_child(segment)
	grid.place_entity(segment, grid.world_to_map(segment_pos))
```

可以看到，该函数做了两件事：

1. 在节点中添加segment节点
2. 将segment放置到它的位置上

保存运行测试一下。



## 游戏结束

我们规定，蛇头超出屏幕或是撞到蛇身上，都视为死亡，我们分别处理。

### 碰到蛇身

同样在**Grid**脚本的**move_entity_in_direction**函数（它处理蛇头的移动），将处理碰到食物部分的代码改为：

```
var entity_of_new_cell: Node2D = get_entity_of_cell(new_grid_pos)
	if entity_of_new_cell != null:
		if entity_of_new_cell.is_in_group("Food"):
			emit_signal("eat_food", entity_of_new_cell, entity)
		elif entity_of_new_cell.is_in_group("Snake"):
			init_grid()
			emit_signal("game_over")
```

可以看到，增加了一个新的条件，如果碰到group为**Player**（即蛇身）的实体后，调用**init_grid**函数初始化地图数据，并发射号**game_over**。因此，我们还要定义一个信号，就在**Grid**脚本里：

```
signal game_over()
```

接着打开**MainScene** Scene，选中**Grid**节点，打开**Signal**面板，**game_over**信号已经出现在里面，双击它为**MainScene**脚本添加新方法，在这个方法里，我们将删除当前的所有对象并初始化它们（蛇和食物）：

```
func _on_Grid_game_over():
	delete_entities_of_group("Food")
	delete_entities_of_group("Snake")
	
	init_entities()
```

这里我们有一个新方法**delete_entities_of_group**，所以，需要创建它：

```
func delete_entities_of_group(group_name: String):
	var entities: Array = get_tree().get_nodes_in_group(group_name)
	for entity in entities:
		entity.queue_free()
```

该函数先是获取所有在group_name的实体，再一个一个删除它们。所以，我们还需要给Snake添加group。

打开**Snake** Scene和**Tail** Scene，选中它们的根节点，为他们添加同样的Group **Snake**。

### 超出屏幕

同样是在**Grid**脚本的**move_entity_in_direction**函数里，添加边界检查代码：

```
if !is_cell_inside_bounds(new_grid_pos):
		init_grid()
		emit_signal("game_over")
		return
```

**is_cell_inside_bounds**函数用于检查指定位置是否超出边界，我们先添加此函数：

```
func is_cell_inside_bounds(cell_pos: Vector2):
	if cell_pos.x < grid_size.x and cell_pos.y >= 0 and cell_pos.y < grid_size.y and cell_pos.x >= 0:
		return true
		
	return false
```

简单易懂。保存运行测试一下，各部分都已经正常工作了吧。



## 小结

好了，Godot的贪食蛇我们就在这里结束了。不管游戏多简单，基础内容都很重要，在本教程里，我们学习了网格的构建，数组的使用，自定义信号等，相信这些内容在别的游戏中都可能会用到，希望能够为你带来帮助。

