# 贪食蛇 - Part 1 - Godot3教程

![](https://www.colorgamer.com/usr/uploads/2020/08/3005092090.png)

贪食蛇，作为最入门的游戏的之一，在很多的程序教程里都会有讲。它的原理非常简单，但不同语言或引擎涉及的内容自然有所不同。所以，在本教程里，我们将了解：

1. 基本数据结构Array的应用
2. 预加载scene
3. 动态添加scene实例
4. 网格的设置，位置与移动
5. 自定义信号



## 目录

1. 关于贪食蛇
2. 游戏地图
   1. 创建Grid Scene
   2. 初始化Grid
   3. 绘制网格边框
3. 创建蛇头，蛇身，食物
6. 初始化蛇



## 关于贪食蛇

如果你对贪食蛇的机制比较了解，可以直接跳到**游戏界面**。



## 游戏地图

根据我们对贪食蛇的定义，它的地图其实就是网格，如下图：

![](https://www.colorgamer.com/usr/uploads/2020/08/3207830552.png)

因此，我们可以使用Tilemap来处理它



### 创建Grid Scene

新建场景，使用**Tilemap**作为根节点，重命名为**Grid**。

- 设置**Cell > Size**的**x**和**y**都为32

![](https://www.colorgamer.com/usr/uploads/2020/08/1575475685.png)

可以看到，当Cell size为32*32时，屏幕可视范围并没有包含所有的单元格。解决这个问题，只需要根据单元格大小和长高计算出屏幕大小即可。如果我们的屏幕长为32个单元格，高为19个单元格，那屏幕长为32 * 32 = 1024，高为32 * 19 = 608。

- 点击**Project > Project Settings...**，打开设置窗口
- 找到Windows，将**Size > Width**设置为1024，**Height**设置为608
- 取消**Resizable**的选中状态

![](https://www.colorgamer.com/usr/uploads/2020/08/2128071663.png)



### 初始化Grid

有了Grid后，我们还需要有存放网格的数据对象。因为我们要处理的都是数据，并按数据将需要的对象渲染到界面上。

选中**Grid**根节点，为它创建一个脚本，命名为**Grid**。接着，我们先从变量开始。

```
class_name Grid

onready var grid_size = Vector2(32, 19)

var grid: Array
```

- **class_name**: 申明类明，以便其它对象引用
- **grid_size**: 网格大小。初始化x向量32，y向量19。意思是x轴方向32格，y轴方向19格
- **grid**: 用于存储网格数据的二维数组

接着创建**init_grid**函数，它用于将**grid**数组按网格数填满

```
func init_grid():
	grid = []
	for x in range(grid_size.x):
		grid.append([])
		for y in range(grid_size.y):
			grid[x].append(null)
```

最后的grid数组，只是代表网格，每个格子的值默认都是null，表示没有内容。

接着，在**_ready**（它会在节点初始化的时候被调用一次）函数里调用它。

```
func _ready():
	init_grid()
```

这时，点击**Play Scene**按钮，可以看到窗口里没有任何东西。但其实数据已经初始化了。接着我们绘制网格边框让它明显一点。



### 绘制网格边框

我们知道，地图是格子组成的，网格边框其实就是这些格子的边框，所以，只需要按格子的右侧和下底部（也就是每一行和第一列的底部和右侧）画一条线，就可以呈现网格状了。

同样在**Grid**脚本里。首先，先定义变量

```
export var line_color: Color
export var line_thickness: int = 1
```

- **export**：其实没有什么作用，只是让它可以映射到**Inspector**里，方便设置值
- **line_color**：颜色
- **line_thickness**：粗细。单位是像素

接着，添加**_draw**函数，用于画线

```
func _draw():
	for x in range(grid_size.x):
		var start_point: Vector2 = Vector2(x * cell_size.x , 0)
		var end_point: Vector2 = Vector2(x * cell_size.x, grid_size.y * cell_size.y)
		draw_line(start_point, end_point, line_color, line_thickness)
		
	for y in range(grid_size.y):
		var start_point: Vector2 = Vector2(0, y * cell_size.y)
		var end_point: Vector2 = Vector2(grid_size.x * cell_size.x, y * cell_size.y)
		draw_line(start_point, end_point, line_color, line_thickness)
```

两点决定一条线，所以，我们只需要找到开始和结束点，就可以划出一条线。根据之前我们的理解，每一行即grid二维数组一维第一格即为开始的点，它的坐标加上单元格的高度，即**x * cell_size.x**，y = 0，因为它就在y轴上。而结束的点，x轴的向量不变，但y轴的向量需要用网格数乘以单元格的高，即**grid_size.y * cell_size .y**。列呢则和行相反。

这时点击**Play Scene**，我们先看到游戏地图开始时截图。



## 创建蛇头，蛇身和食物

### 创建蛇头

创建一个新Scene，使用**Node2D**作为根节点，重命名为**Snake**

- 添加子节点**Area2D**
- 为**Area2D**添加子节点**Polygon2D**
- 选中**Polygon2D**，设置**Data > Polygon > Size**为4，0.x=-32,0.y=-32，1.x = 32, 1.y = -32, 2.x = 32, 2.y = 32, 2.x = -32, 2.y = 32
- 设置**Color**为你喜欢的颜色。我们这里用黑色

![](https://www.colorgamer.com/usr/uploads/2020/08/2210863429.png)



### 创建蛇身和食物

在**FileSystem**里，右键点击**Snake.tscn**，选择**duplicate...**，复制为**Tail.tscn**和**Food.tscn**。分别设置它们的颜色，我们这里Tail使用深灰色，Food使用绿色。



## 初始化蛇和食物

有了地图，蛇和食物后。现在，我们要来将它们连接起来。

首先，创建一个新Scene。

- 以**Node2D**为根节点，重命名为**MainScene**。
- 保存场景
- 将**Grid** Scene拖入场景里，确保它的position为0, 0

接着，为**MainScene**添加脚本，命名为**MainScene**。仍然我们从变量开始。

```
const scene_snake = preload("res://Snake.tscn")
const scene_food = preload("res://Food.tscn")

onready var grid: Grid = get_node("Grid")

var snake: Node2D
```

- **preload**：预加载场景，将Snake和Food都先加载进来
- **grid**：为**Grid**节点的引用
- **snake**：初始化为**Node2D**类型的变量

然后，创建**init_entities**函数，用于初始化实例

```
func init_entities():
	snake = scene_snake.instance() as Node2D
	add_child(snake)
```

- **scene_snake.instance()**：实例化snake
- **add_child**：将snake添加进场景里

运行一下（以**MainScene**为默认场景），可以看到咱们黑色的蛇头已经出现在了左上角第一格里

![](https://www.colorgamer.com/usr/uploads/2020/08/3131048256.png)

但我们希望每次它都出现在随机的位置，所以来到**Grid**脚本，添加**place_entity_at_random_pos**函数。

```
func place_entity_at_random_pos(entity: Node2D):
	var has_random_pos: bool = false
	var random_grid_pos: Vector2
	
	while has_random_pos == false:
		var temp_pos: Vector2 = Vector2(randi() % int(grid_size.x), randi() % int(grid_size.y))
		if get_entity_of_cell(temp_pos) == null:
			random_grid_pos = temp_pos
			has_random_pos = true
			
	place_entity(entity, random_grid_pos)
```

首先，我们需要知道，在网格里放置任何一个对象，那个网格都必须是空的。所以，当通过随便数获取到一个网格的位置时，需要先检查这一格是不是空(记得初始化时的null吗)，如果不是需要继续获取一个随机位置。那么**has_random_pos**就是用来跳出循环的。当获取到可用的随机位置时，调用**place_entity**函数来放置实体对象到网格里。

**get_entity_of_cell**函数：

```
func get_entity_of_cell(grid_pos: Vector2):
	return grid[grid_pos.x][grid_pos.y] as Node2D
```

直接返回指定x,y的grid数组的数据，如果不为null，则已有实体。

**place_entity**函数：

```
func place_entity(entity: Node2D, grid_pos: Vector2):
	set_entity_in_cell(entity, grid_pos)
	entity.set_position(map_to_world(grid_pos))
```

这里需要分两步：

1. 将grid数组里对应位置的值附上实体
2. 将实体的position设置为网格的坐标

**map_to_world**将网格的位置转换为网格所在位置的坐标。

**set_entity_in_cell**函数：

```
func set_entity_in_cell(entity: Node2D, grid_pos: Vector2):
	grid[grid_pos.x][grid_pos.y] = entity
```

最后，回到**MainScene**脚本，在**init_entities**函数**add_child(snake)**之后，加上：

```
grid.place_entity_at_random_pos(snake)
```

保存后，点击运行，可以看到，有两个问题：

1. **Snake**并没有正确的出现在网格里
2. **Snake**被放大到64*64
3. 每次运行**Snake**都出现在相同的位置

之所以没有出现在网格里，是因为**map_to_world**在转换坐标的时候，是网格的左上角坐标，而我们的**Snake**的锚点在它的正中央。所以，要解决这个问题有两个处理办法：

1. 在计算坐标的时候，加上半个网格的大小
2. 在制作**Snake**的时候，将锚点称到它的左上角

**加半个网格大小：**

来到**Grid**脚本，定义一个变量：

```
onready var half_cell_size = get_cell_size() / 2
```

接着到**place_entity**函数，修改：

```
entity.set_position(map_to_world(grid_pos) + half_cell_size)
```

问题2，原因应该和使用了Polygon2D有关，需将它的**Scale**设置成0.5, 0.5

回到问题3，之所以每次运行都出现在同一位置，有过随机数经验的开发者应该都知道，需要Randomizes the seed，所以，来到**MainScene**脚本，在**_ready**函数的开始，添加：

```
randomize()
```

保存后，再次运行，snake已经正确的出现在它可能出现的位置了。



## 继续Part 2

那么本章，介绍了初始化地图，蛇，并让它随机出现在不同的网格里。下一章，我们将继续Snake的移动，吃食物，增加蛇身和死亡检测。

