# 如何制作策略游戏 - Part 2 - Godot3教程

![](https://www.colorgamer.com/usr/uploads/2020/08/3813418922.png)

> Godot游戏开发新手教程 #5
>
> 翻译自：[Godot Game Development for Beginner #5](https://godottutorials.pro/godot-strategy-game-tutorial-part-2/)
>
> 作者：Daniel Buckley



## 目录

1. 介绍
2. 项目文件
3. 完善Map脚本
4. 游戏管理脚本
5. UI脚本
6. 连接所有内容
7. 结论



## 介绍

欢迎回到在Godot引擎中创建策略游戏的第二部分。

在Part1里，我们向你展示了如何通过设置可高亮的tiles，buildings和基础UI来在Godot中创建你的第一个策略游戏，这些将告诉玩家有关资源管理的重要信息。总的来说，有了一个好的开始，接下来我们将完善它！

当然，在Part 2，还有很多的工作要做。需要完善地图系统，正确的设置UI，给予放置建筑物的能力，甚至实现回合制的整体游戏流程。那么，如果你准备好了，让我们来完成这个资源管理游戏。



## 项目文件

对于本项目，我们需要一些素材，如果精灵，字体。它们来自于[kenney.nl](https://kenney.nl/)和[Google Fonts](https://fonts.google.com/)。当然，你也可以使用自己的资源，但在本教程里，我们将使用它们。

- 下载精灵和字体素材以及完整的项目 [点这里](https://godottutorials.pro/godot-strategy-game-tutorial/)



## 完善Map脚本

首先，回到**Map**脚本，**place_building**函数在我们放置建筑到Tile上时被调用。

```
func place_building(tile, texture):
	tilesWithBuildings.append(tile)
	tile.place_building(texture)
	
	disable_tile_highlights()
```

接着在**_ready**函数（在节点初始化时被调用一次）中，我们需要获取所有在"Tiles" group中的tiles，并设置初始的基地建筑。

```
func _ready():
	allTiles = get_tree().get_nodes_in_group("Tiles")
	#
	for x in range(allTiles.size()):
		if allTiles[x].startTile == true:
			place_building(allTiles[x], BuildingData.base.iconTexture)
```

回到**MainScene**，选择中间的tile，并勾选它的**Start Tile**。

![](https://www.colorgamer.com/usr/uploads/2020/09/3588554278.png)

现在，如果按下运行按钮，可以看到一个基地建筑放在了中间的tile上。

![](https://www.colorgamer.com/usr/uploads/2020/09/2519048078.png)



## 游戏管理脚本

**GameManager**脚本将用于管理我们的资源和状态。来到**MainScene**并选中**MainScene**节点。创建一个新的脚本，命名为**GameManager**。建立如下变量：

```
var curFood: int = 0
var curMetal: int = 0
var curOxygen: int = 0
var curEnergy: int = 0

var foodPerTurn: int = 0
var metalPerTurn: int = 0
var oxygenPerTurn: int = 0
var energyPerTurn: int = 0

var curTurn: int = 1

var currentlyPlacingBuilding: bool = false
var buildingToPlace: int

onready var ui: Node = get_node("UI")
onready var map: Node = get_node("Tiles")
```

**on_select_building**函数在我们按下3个建筑按钮时会被调用。这将在创建UI脚本后连接。

```
func on_select_building(buildingType):
	currentlyPlacingBuilding = true
	buildingToPlace = buildingType
	
	map.highlight_available_tiles()
```

**add_to_resource_per_turn**函数，每回合添加给定数量的资源到给定的资源里。

```
func add_to_resource_per_turn(resource, amount):
	if resource == 0:
		return
	elif resource == 1:
		foodPerTurn += amount
	elif resource == 2:
		metalPerTurn += amount
	elif resource == 3:
		oxygenPerTurn += amount
	elif resource == 4:
		energyPerTurn += amount
```

**place_building**函数，在放置建筑到Tile上时被调用。

```
func place_building(tileToPlaceOn):
	currentlyPlacingBuilding = false
	var texture: Texture
	#
	if buildingToPlace == 1:
		texture = BuildingData.mine.iconTexture
		#
		add_to_resource_per_turn(BuildingData.mine.prodResource, BuildingData.mine.prodResourceAmount)
		add_to_resource_per_turn(BuildingData.mine.upkeepResource, -BuildingData.mine.upkeepResourceAmount)
	#
	if buildingToPlace == 2:
		texture = BuildingData.greenhouse.iconTexture
		#
		add_to_resource_per_turn(BuildingData.greenhouse.prodResource, BuildingData.greenhouse.prodResourceAmount)
		add_to_resource_per_turn(BuildingData.greenhouse.upkeepResource, -BuildingData.greenhouse.upkeepResourceAmount)
	#
	if buildingToPlace == 3:
		texture = BuildingData.solarpanel.iconTexture
		#
		add_to_resource_per_turn(BuildingData.solarpanel.prodResource, BuildingData.solarpanel.prodResourceAmount)
		add_to_resource_per_turn(BuildingData.solarpanel.upkeepResource, -BuildingData.solarpanel.upkeepResourceAmount)
	#
	map.place_building(tileToPlaceOn, texture)
```

最后，**end_turn**函数，将在我们按下end turn按钮时被调用。

```
func end_turn():
	curFood += foodPerTurn
	curMetal += metalPerTurn
	curOxygen += oxygenPerTurn
	curEnergy += energyPerTurn
	#
	curTurn += 1
```

现在，我们已经有了GameManager class，但是还没有任何的作用。为了将所有内容连接在一起，我们需要创建**UI**脚本。



## UI脚本

在**UI** Scene里，选中UI节点，创建一个新的脚本，命名为**UI**。

```
onready var buildingButtons: Node = get_node("BuildingButtons")
onready var foodMetalText: Label = get_node("FootMetalText")
onready var oxygenEnergyText: Label = get_node("OxygenEnergyText")
onready var curTurnText: Label = get_node("TurnText")
onready var gameManager: Node = get_node("/root/MainScene")
```

首先是**on_end_turn**函数。它将在回合结束时被调用，用于重置UI。

```
func on_end_turn():
	curTurnText.text = "Trun: " + str(gameManager.curTurn)
	buildingButtons.visible = true
```

接着是**update_resource_text**函数，它将更新两个资源标签，显示玩家当前的资源值。

```
func update_resource_text():
	var foodMetal = ""
	foodMetal += str(gameManager.curFood) + "(" + ("+" if gameManager.foodPerTurn >= 0 else "") + str(gameManager.foodPerTurn) + ")"
	foodMetal += "\n"
	foodMetal += str(gameManager.curMetal) + "(" + ("+" if gameManager.metalPerTurn >= 0 else "") + str(gameManager.metalPerTurn) + ")"
	#
	foodMetalText.text = foodMetal
	#
	var oxygenEnergy = ""
	oxygenEnergy += str(gameManager.curOxygen) + "(" + ("+" if gameManager.oxygenPerTurn >= 0 else "") + str(gameManager.oxygenPerTurn) + ")"
	oxygenEnergy += "\n"
	oxygenEnergy += str(gameManager.curEnergy) + "(" + ("+" if gameManager.energyPerTurn >= 0 else "") + str(gameManager.energyPerTurn) + ")"
	oxygenEnergyText.text = oxygenEnergy
```

接着我们连接按钮。在**UI** Scene，对EndTurnButton， MineButton, GreenhouseButton和SolarPanelButton执行以下操作：

1. 选中button节点
2. 双击**pressed**信号（会在按下按钮时被调用）
3. 连接它到UI脚本

![](https://www.colorgamer.com/usr/uploads/2020/09/1568667409.png)

回到脚本，我们需要添加4个新的函数。让我们从3个建筑按钮开始。

```
func _on_MineButton_pressed():
	buildingButtons.visible = false
	gameManager.on_select_building(1)


func _on_GreenhouseButton_pressed():
	buildingButtons.visible = false
	gameManager.on_select_building(2)


func _on_SolarPanelButton_pressed():
	buildingButtons.visible = false
	gameManager.on_select_building(3)
```

接着是end turn按钮的函数。

```
func _on_EndTurButton_pressed():
	gameManager.end_turn()
```



## 连接所有内容

现在，有了UI脚本，回到**Tile**脚本，**_on_Tile_input_event**函数。

```
func _on_Tile_input_event(viewport, event, shape_idx):
	if event is InputEventMouseButton and event.pressed:
		var gameManager = get_node("/root/MainScene")
		if gameManager.currentlyPlacingBuilding and canPlaceBuilding:
			gameManager.place_building(self)
```

接着，来到**GameManager**脚本，创建**_ready**函数，用于初始化UI。

```
func _ready():
	ui.update_resource_text()
	ui.on_end_turn()
```

在**end_turn**函数的最后，添加更新UI的代码。

```
ui.update_resource_text()
ui.on_end_turn()
```

最后，在**place_building**函数的最后，更新资源文本。

```
ui.update_resource_text()
```

现在，点击play，测试一下！



## 结论

祝贺你完成了本教程！

你刚刚在Godot里创建了一个2D，回合制的策略游戏。在此过程中，涵盖了很多的内容，从设置生产和消耗资源的对象，到创建一个基于Tile的地图，它提供了可视化的放置建筑提示。此外，在使用回合制机制的同时，我们还获得了许多其它策略游戏的关键组成部分！

以此为基础，你可以扩展学到的内容，添加更多的系统，让它们在现有系统上运行，或者开始一个新的策略游戏项目。无论如何，非常感谢你关注本教程，并祝你在未来的Godot游戏开发中一切顺利。